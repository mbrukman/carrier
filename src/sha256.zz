using "../3rdparty/hacl-star/snapshots/hacl-c/Hacl_SHA2_256.h" as hacl;
using <assert.h>::{assert};
using <string.h>::{memset};
using std;

pub const usize HASHLEN   = 32;
pub const usize BLOCKLEN  = 64;

export fn hashlen() -> usize {
    return HASHLEN;
}

export fn blocklen() -> usize {
    return BLOCKLEN;
}

export struct Sha256 {
    u32 mut state[137];
    u8  mut block[64];
    u8  mut at;
}

export fn init(Sha256 mut set<isinit> *self) {
    hacl::Hacl_SHA2_256_init(self->state);
    self->at = 0;
}

export fn update(Sha256 mut *self, u8 * mut data, usize mut l)
    where len(data) >= l
{
    static_attest(len(self->block) > self->at);
    static_attest(len(self->block) > 64 - self->at);

    for (;;) {
        usize fill = as<usize>(64 - self->at);
        if l >= fill {
            static_attest(len(data) > fill);
            std::memcpy(self->block + self->at, data, fill);
            hacl::Hacl_SHA2_256_update(self->state, self->block);
            l    -= fill;
            data = data + fill;
            self->at = 0;
        } else {

            static_attest(len(self->block) > (usize)self->at);
            u8 mut *dst = self->block + self->at;
            static_attest(len(dst) > l);
            static_attest(safe(data));
            static_attest(len(data) > l);
            std::memcpy(dst, data, l);
            self->at += (u8)l;
            break;
        }
    }
}

export fn finish(Sha256 mut unset<isinit> *self, u8 mut *out) {
    hacl::Hacl_SHA2_256_update_last(self->state, self->block, self->at);
    hacl::Hacl_SHA2_256_finish(self->state, out);
}

pub fn hkdf(
    u8 * chaining_key,
    u8 * input_key_material,
    usize input_key_material_len,
    u8 mut* unsafe out1,
    u8 mut* unsafe out2,
    u8 mut* unsafe out3)

    where HASHLEN <= len(chaining_key)
    where out1 == 0 || HASHLEN <= len(out1)
    where out2 == 0 || HASHLEN <= len(out2)
    where out3 == 0 || HASHLEN <= len(out3)
    where input_key_material_len <= len(input_key_material)

{
    u8 mut temp_key[HASHLEN] = {0};
    u8 mut temp_hash[HASHLEN + 1] = {0};

    // temporary hashing key
    hmac(chaining_key, HASHLEN, input_key_material, input_key_material_len, temp_key);

    // output 1
    if out1 == 0 {
        return;
    }
    static_attest(safe(out1));
    temp_hash[0] = 0x01;
    hmac(temp_key, HASHLEN, temp_hash, 1, out1);

    //output 2
    if out2 == 0 {
        return;
    }
    static_attest(safe(out2));
    std::memcpy(temp_hash, out1, HASHLEN);
    temp_hash[HASHLEN] = 0x02;
    hmac(temp_key, HASHLEN, temp_hash, HASHLEN + 1, out2);

    //output 3
    if out3 == 0 {
        return;
    }
    static_attest(safe(out3));
    std::memcpy(temp_hash, out2, HASHLEN);
    temp_hash[HASHLEN] = 0x03;
    hmac(temp_key, HASHLEN, temp_hash, HASHLEN + 1, out3);
}


pub fn hmac(u8 *key, usize keylen, u8* data, usize datalen, u8 mut *out)
    where keylen  <= len(key)  && keylen < BLOCKLEN
    where datalen <= len(data) && keylen < BLOCKLEN
{
    assert(keylen <= BLOCKLEN);

    u8 mut ipad [BLOCKLEN] = {0};
    memset(ipad, 0x36, BLOCKLEN);

    u8 mut opad [BLOCKLEN] = {0};
    memset(opad, 0x5c, BLOCKLEN);

    for (usize mut i = 0; i < keylen ; i++) {
        ipad[i] = ipad[i] ^ key[i];
        opad[i] = opad[i] ^ key[i];
    }

    u8 mut inner_output[HASHLEN] = {0};
    Sha256 mut s1 = {0};
    init(&s1);
    update(&s1, ipad, BLOCKLEN);
    update(&s1, data, datalen);
    finish(&s1, inner_output);

    Sha256 mut s2 = {0};
    init(&s2);
    update(&s2, opad, BLOCKLEN);
    update(&s2, inner_output, HASHLEN);
    finish(&s2, out);
}
